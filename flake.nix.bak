{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";

    android-nixpkgs = {
      url = "github:tadfisher/android-nixpkgs";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      android-nixpkgs,
    }:
    let
      systems = [ "x86_64-linux" ];
      forAllSystems = nixpkgs.lib.genAttrs systems;

      pkgsFor =
        system:
        import nixpkgs {
          inherit system;
          config = {
            allowUnfree = true;
            android_sdk.accept_license = true;
          };
        };

      androidSdkFor =
        system:
        android-nixpkgs.sdk.${system} (
          sdkPkgs: with sdkPkgs; [
            build-tools-35-0-0
            build-tools-34-0-0
            cmdline-tools-latest
            platform-tools
            platforms-android-34
            platforms-android-35
            ndk-25-1-8937393
            ndk-26-1-10909125
          ]
        );

      mkShellFor =
        system:
        let
          pkgs = pkgsFor system;
          androidSdk = androidSdkFor system;

          # Base packages
          basePackages = with pkgs; [
            nodejs_22
            yarn
            androidSdk
            jdk17
            git
            curl
            unzip
            corepack
          ];

          # Scripts
          shellScripts = pkgs.writeScriptBin "setup-scripts" ''
            #!${pkgs.stdenv.shell}

            android-init() {
              # Install dependencies
              yarn install
              # Generate proto files
              yarn gen-proto
              node node_modules/react-native-turbo-lnd/src/fetch-lnd.js
              echo "sdk.dir=$ANDROID_HOME" > android/local.properties
              # Download the Lndmobile.aar file and place it in the directory
              curl -L "https://github.com/hsjoberg/blixt-wallet/releases/download/v0.7.0/Lndmobile.aar" \
                -o android/app/lndmobile/Lndmobile.aar
              echo "Lndmobile.aar downloaded and placed in android/app/lndmobile/"
            }

            android-unsigned-apk() {
              yarn android:mainnet-unsigned
            }

            # Export functions
            export -f android-init
            export -f android-unsigned-apk
          '';

          # Shell hook
          commonHook = ''
            # Environment variables
            export GREET="devenv"
            export YARN_CHECKSUM_BEHAVIOR="reset"
            export LC_ALL=en_US.UTF-8
            export LANG=en_US.UTF-8

            # Source the scripts
            source ${shellScripts}/bin/setup-scripts

            # Enable corepack
            corepack enable
          '';

        in
        pkgs.mkShellNoCC {
          buildInputs = basePackages;
          shellHook = commonHook;
        };
    in
    {
      devShells = forAllSystems (system: {
        default = mkShellFor system;
      });
    };
}
